#!/usr/bin/env perl

# mmtop10.pl
# v1.0 - 12/9/2011
# Copyright (c) 2011 Carl Gorringe <car1@gorringe.org>
# Licensed under the MIT license
# https://github.com/cgorringe/mmStats
#
# About:
#   This downloads from a Mailman mailing list archive at the given URL,
#   then outputs the Top-10 users by number of posts in each month, and
#   the overall total.
#
# Usage:  
#   mmtop10.pl URL > output.html
#
#------------------------------------------------------------------------------
# Installing Instructions
#
# You may need to run the following first:
#   sudo perl -MCPAN -e 'install IO::Socket::SSL'
#
# * To check to see if a Perl Module is installed, type:
#   perl -Mfoo::bar -e 1
#   where "foo::bar" is the name of the module to check.
# * To install a Perl Module from the CPAN repository:
#   sudo perl -MCPAN -e 'install foo::bar'
#   where "foo::bar" is the name of the module to install.
#
# Modules that you may need to install:
#   IO::Socket::SSL
#------------------------------------------------------------------------------

use strict;
use LWP::Simple qw(get getprint);
use LWP::UserAgent;
use IO::Socket::SSL;

my $debug_flag = 1;

#------------------------------------------------------------------------------
# Download and Parse the Mailman archive index file and return a list 
# of URL strings of each month's author.html URL.
#   Input: $url of index directory (must end in a slash!)
# Returns: (\@urlList, \@monthList)

sub parseIndexPage($)
{
  my $url = shift;
  my @urlList = ();
  my @monthList = ();

  # download index page
  my $ua = LWP::UserAgent->new(ssl_opts => { verify_hostname => 1 });
  my $response = $ua->get($url);

  if ($response->is_success) {
    my $text = $response->decoded_content;
    #print $text;  # test

    ## Example pattern to search for:
    ## href="2011-December/author.html"

    @monthList = $text =~ /href="(.*)\/author.html"/g;
    my $u;
    foreach my $m (@monthList) {
      $u = $url . $m . '/author.html';
      push @urlList, $u;
    }
  }
  else {
    print STDERR "ERROR getting $url\n" if ($debug_flag);
    print STDERR "      status code: ". $response->status_line ."\n";
  }

  return (\@urlList, \@monthList);
}

#------------------------------------------------------------------------------
# Takes a hash array tally of {user => numPosts} and creates 2 arrays of 
# the top-10 users, sorted by numPosts in descending order.
#   Input: \%users
# Returns: (\@userNames, \@numPosts)

sub convertTallyToTop10($)
{
  my $usersHash_ref = shift;
  my @userNames = ();
  my @numPosts = ();

  # convert hash array into an array of tuples
  my @users2 = ();
  foreach my $u (keys %$usersHash_ref) {
    push @users2, [$usersHash_ref->{$u}, $u];
  }

  # sort users by most posts
  my @users3 = sort { $b->[0] <=> $a->[0] } @users2;

  # split into 2 arrays for returning
  foreach my $u (@users3) {
    push @numPosts, $u->[0];
    push @userNames, $u->[1];
  }

  return (\@userNames, \@numPosts);
}

#------------------------------------------------------------------------------
# Download and Parse a month's author archive page.
#   Input: ($url of the html page to download, \%totalUsers tally)
# Returns: \%users = hash tally of {user => numPosts}

sub parseMonthPage($$)
{
  my ($url, $totalUsers_ref) = @_;
  my ($userNames_ref, $numPosts_ref);
  my %users;

  # download page
  my $ua = LWP::UserAgent->new(ssl_opts => { verify_hostname => 1 });
  my $response = $ua->get($url);

  if ($response->is_success) {
    my $text = $response->decoded_content;

    # tally user names
    my @tempUsers = $text =~ /\<I\>(.*)/g;
    foreach my $u (@tempUsers) {

      # month tally
      if ($users{$u}) { $users{$u} += 1; }
      else { $users{$u} = 1; }

      # total tally
      if ($totalUsers_ref->{$u}) { $totalUsers_ref->{$u} += 1; }
      else { $totalUsers_ref->{$u} = 1; }
    }

    # ($userNames_ref, $numPosts_ref) = convertTallyToTop10(\%users);
  }
  else {
    print STDERR "ERROR getting $url\n" if ($debug_flag);
    print STDERR "      status code: ". $response->status_line ."\n";
  }

  # return ($userNames_ref, $numPosts_ref);
  return \%users;
}

#------------------------------------------------------------------------------
# Return HTML header and footer.

sub htmlHeader($$)
{
  my ($url, $listTitle) = @_;

  return <<HTMLCODE;
<!DOCTYPE html>
<html>
<head>
 <title>Top10 - $listTitle</title>
 <link rel="stylesheet" href="mmstyle.css" type="text/css" />
</head>
<body>
<a name="top"></a>
<h1>Top 10 Posters</h1>
<h2><a href="$url">$listTitle</a></h2><br/>
<div style="width:100%; text-align:center"><a href="#bottom">Go to Top 10 Totals</a></div>
HTMLCODE
}

sub htmlFooter()
{
  return <<HTMLCODE;
<div class="clear"></div>
<div style="width:100%; text-align:center"><a href="#top">Go to Top</a></div>
<a name="bottom"></a>
<br/><br/><br/>
<div class="copyright">Page Generated by <a href="https://github.com/cgorringe/mmStats">mmStats</a>.</div><br/>
</body>
</html>
HTMLCODE
}

#------------------------------------------------------------------------------
# Return HTML code for a single month's top 10 list as a table.

sub htmlMonthTable
{
  my $userNames_ref = shift;
  my $numPosts_ref  = shift;
  my $monthTitle    = shift;
  my $url           = shift;  # title link (optional)
  my $class         = shift;  # table class (optional)
  my $id            = shift;  # table id (optional)

  my $maxposts = $$numPosts_ref[0];
  my ($width, $ret);

  # HTML for table
  $ret  = '<div class="col"><table';
  $ret .= ' class="'. $class .'"' if $class;
  $ret .= ' id="'. $id .'"' if $id;
  $ret .= '><caption>';
  if ($url) { $ret .= '<a href="'. $url .'">'. $monthTitle .'</a>'; }
  else      { $ret .= $monthTitle }
  $ret .= "</caption>\n";

  # print top 10 users
  for (my $i=0; $i < scalar @$userNames_ref; $i++) {
    if ($i == 10) { last; }  # stop at 10th user

    $width = ($maxposts == 0) ? 100 : int($$numPosts_ref[$i] / $maxposts * 100);

    $ret .= '<tr><td>'. ($i+1) .'</td><td>'. $$userNames_ref[$i]
           .'</td><td><div style="width:'. $width .'%">'. $$numPosts_ref[$i] 
           ."</div></td></tr>\n";
  }

  $ret .= "</table>\n</div>\n";
  return $ret;
}

#------------------------------------------------------------------------------
# Extracts the Mailman list title from the given URL.

sub extractListTitle($)
{
  my $url = shift;
  $url =~ m{([^\/]*)\/$};
  return $1;
}

#------------------------------------------------------------------------------
# Main Program
{
  my $url = "https://www.noisebridge.net/pipermail/noisebridge-discuss/";

  print STDERR "Mailman Top Ten Posters Generator v1.0\n".
               "(c) 2011 Carl Gorringe <car1\@gorringe.org>\n\n";

  if (scalar @ARGV != 1) {
    print STDERR "Usage:\n  $0 URL > output.html\n";
    print STDERR "Example:\n  $0 $url > top10.html\n";
    exit(1);
  }

  # get URL, and make sure it ends in a slash
  $url = $ARGV[0];
  if (substr($url, -1, 1) ne '/') { $url .= '/'; }

  # extract the name of the list
  my $listTitle = extractListTitle($url);

  print STDERR "Downloading & Parsing:\n" if ($debug_flag);
  print STDERR "  $url\n" if ($debug_flag);
  my ($urlList_ref, $monthList_ref) = parseIndexPage($url);
  my %totalUsers;

  # start printing HTML
  print htmlHeader($url, $listTitle);

  # get every author.html page
  for (my $i=0; $i < scalar @$urlList_ref; $i++) {
    my $url2 = $$urlList_ref[$i];

    print STDERR "  $url2\n" if ($debug_flag);
    # my $monthUsers_ref = parseMonthPage($url2, \%totalUsers);
    my ($userNames_ref, $numPosts_ref) = convertTallyToTop10( parseMonthPage($url2, \%totalUsers) );

    ### TEST ###
    #print "<h2>$$monthList_ref[$i]</h2>\n";
    #for (my $j=0; $j < scalar @$userNames_ref; $j++) {
    #  if ($j == 10) { last; }  # only print top 10
    #  print "$$numPosts_ref[$j] \t $$userNames_ref[$j] \n";
    #}

    # break tables into groups of 3 per row
    print '<div class="clear"></div>'."\n" if ($i % 3 == 0);

    # output month's top-10 table
    print htmlMonthTable($userNames_ref, $numPosts_ref, $$monthList_ref[$i], $url2, 'tabMonth tabAll');
  }

  # output Top-10 Totals table
  my ($totalUserNames_ref, $totalNumPosts_ref) = convertTallyToTop10(\%totalUsers);
  print '<div class="clear"></div>'."\n" if (scalar @$urlList_ref % 3 == 0);
  print htmlMonthTable($totalUserNames_ref, $totalNumPosts_ref, 'Totals', '', 'tabTotal tabAll', 'idTotal');

  # done
  print htmlFooter();
  print STDERR "Done.\n" if ($debug_flag);
}

